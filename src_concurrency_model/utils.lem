(*=============================================================================================*)
(*                                                                                             *)
(*                rmem executable model                                                        *)
(*                =====================                                                        *)
(*                                                                                             *)
(*  This file is:                                                                              *)
(*                                                                                             *)
(*  Copyright Christopher Pulte, University of Cambridge                           2016-2018   *)
(*  Copyright Peter Sewell, University of Cambridge                                2014-2015   *)
(*  Copyright Shaked Flur, University of Cambridge                           2014, 2016-2017   *)
(*  Copyright Susmit Sarkar, University of St Andrews                                   2014   *)
(*  Copyright Ohad Kammar, University of Cambridge (when this work was done)            2013   *)
(*                                                                                             *)
(*  All rights reserved.                                                                       *)
(*                                                                                             *)
(*  It is part of the rmem tool, distributed under the 2-clause BSD licence in                 *)
(*  LICENCE.txt.                                                                               *)
(*                                                                                             *)
(*=============================================================================================*)

open import Relation
open import Pervasives_extra

(* Help emacs fontification -*-caml-*- *)

(** Maybe *)

let inline ensure_just (a : maybe 'a) (err : string) : 'a =
  match a with
  | Just a -> a
  | Nothing -> failwith err
  end

(** Lists*)

(* two auxiliary functions for dealing with maps in Isabelle *)
val mapToList: forall 'k 'v. MapKeyType 'k, SetType 'k, SetType 'v => map 'k 'v -> list ('k * 'v)
let {coq;ocaml} mapToList = Map_extra.toList
let {isabelle}  mapToList l = Set_extra.toList (Map.toSet l)

val mapi : forall 'k 'v 'w. MapKeyType 'k, SetType 'k, SetType 'v => ('k -> 'v -> 'w) -> map 'k 'v -> map 'k 'w
let {isabelle;coq} mapi f m =
  Map.fromList (map (fun (k,v) -> (k,f k v)) ((mapToList m) : list ('k * 'v)))
let inline {ocaml} mapi = (Map.mapi)

let inline ensure_singleton (xs : list 'x) (err : string) : 'x = 
  match xs with
  | [] -> failwith err
  | [x] -> x
  | _ -> failwith err
  end

val head : forall 'a. list 'a -> 'a
let head l = ensure_just (index l 0) "head: empty list"

let add_event l e = List.snoc e l
let add_events l es = l ++ es

val bigunionListMap : forall 'a 'b. SetType 'b => ('a -> set 'b) -> list 'a -> set 'b
let rec bigunionListMap f xs =
  match xs with
  | [] -> {}
  | x::xs' -> (f x) union bigunionListMap f xs'
  end


val combinations2 : forall 'a. list 'a -> list ('a * 'a)
let rec combinations2 cs =
  match cs with
  | [] -> []
  | [_] -> []
  | (x::xs) ->
    let pair x y = (x, y) in
    List.map (pair x) xs ++ combinations2 xs
  end

val product : forall 'a 'b. list 'a -> list 'b -> list ('a * 'b)
let rec product xs ys =
  match xs with
  | [] -> []
  | (z::zs) ->
    let pair x y = (x, y) in
    List.map (pair z) ys ++ product zs ys
  end

(** Associative lists *)

(* map key 'k' to value 'v' in associative list 'm' (add the pair or
replace the value of existing key) *)
val updateAssocList : forall 'a 'b. Eq 'a => 'a -> 'b -> list ('a * 'b) -> list ('a * 'b)
let updateAssocList k v m =
  match List.splitWhile (fun (k', _) -> k' <> k) m with
  | (left, (_, _) :: right) -> left ++ ((k, v) :: right)
  | (left, [])              -> (k, v) :: left
  end


let rec mapWithContext_helper f prefix suffix accum =
  match suffix with
  | [] -> List.reverse accum
  | x::xs -> mapWithContext_helper f (prefix ++ [x]) xs ((f prefix x xs) :: accum)
  end


(* apply 'f prefix x suffix' to all (prefix ++ [x] ++ suffix) = l,
 and return the results in a list *)
val mapWithContext : forall 'a 'b. (list 'a -> 'a -> list 'a -> 'b) -> list 'a -> list 'b
let mapWithContext f l = mapWithContext_helper f [] l []


let rec list_minimumBy (ord: 'a -> 'a -> ordering) (l: list 'a) : maybe 'a =
  List.foldl
    (fun min a ->
      match min with
      | Just b  -> if ord a b = LT then Just a else Just b
      | Nothing -> Just a
      end)
    Nothing
    l





(** Functions *)

(*: Map-like functions and Sets :*)
let funupd f x y = fun x' -> if x'=x then y else f x'


(** Relations *)

let acyclic (r : rel 'a 'a) = isIrreflexive (transitiveClosure r)


let rec order_to_list_helper domain order minimals =
  if Set.null order then minimals ++ (Set_extra.toList domain)
  else
    let minimals' = (Relation.relDomain order) \ (Relation.relRange order) in
    order_to_list_helper
      (domain \ minimals')
      {(a, b) | forall ((a, b) IN order) | a NIN minimals'}
      (minimals ++ (Set_extra.toList minimals'))

(* 'order_to_list d o' returns a list l such that e in l iff e in d,
and (a,b) in o --> a appears in l before (closer to the head) b *)
let order_to_list domain order =
  let order = Relation.relRestrict order domain in
  order_to_list_helper domain order []



(** Monads *)

module ListMonad = struct
  val (>>=) : forall 'a 'b. list 'a -> ('a -> list 'b) -> list 'b
  let inline (>>=) = flip List.concatMap

  val (>>) : forall 'b. list unit -> list 'b -> list 'b
  let inline (>>) m n = m >>= fun () -> n

  let inline return x = [x]
  let mzero = []

  val guard : bool -> list unit
  let inline guard b = if b then return () else mzero


  let inline option_guard = function
    | Just a -> return a
    | Nothing -> mzero
  end

end



module SetMonad = struct
  val (>>=) : forall 'a 'b. SetType 'a, SetType 'b => set 'a -> ('a -> set 'b) -> set 'b
  let inline (>>=) = flip Set.bigunionMap

  val (>>) : forall 'b. SetType 'b => set unit -> set 'b -> set 'b
  let inline (>>) m n = m >>= fun () -> n

  let inline return = Set.singleton
  let mzero = Set.empty

  val guard : bool -> set unit
  let inline guard b = if b then return () else mzero

  let inline option_guard = function
    | Just a -> return a
    | Nothing -> mzero
  end

end


module MaybeMonad = struct
  let inline (>>=) = Maybe.bind
  let inline (>>) m n = m >>= fun () -> n

  let inline return = Just
  let mzero = Nothing

  val guard : bool -> maybe unit
  let inline guard b = if b then Just () else Nothing

  let inline option_guard = id
end


(** Debug *)
val nothing1 : forall 'a. 'a -> unit
let nothing1 _ = ()

val nothing2 : forall 'a 'b. 'a -> 'b -> unit
let nothing2 _ _ = ()

val print_integer : integer -> unit
declare ocaml    target_rep function print_integer = `Debug.print_integer`
declare isabelle target_rep function print_integer = `nothing1`


(** relon is a relation over a set (useful when the domain of the
relation does not cover the set, e.g., coherence when there is only one
write) *)
type relon 'a =
  <| relon_set: set 'a;
     relon_rel: Relation.rel 'a 'a;
  |>

let relonEmpty = <| relon_set = {};
                    relon_rel = Relation.relEmpty;
                 |>

let relonFromSetAndRelation s r =
  let () = ensure (Relation.relOver r s)
      ("relonFromSetAndRelation: 'r' is not over 's' (" ^ (show ((Relation.relDefinedOn r) \ s)) ^ ")")
  in
  <| relon_set = s; relon_rel = r |>

(* true iff e1,e2 are in the set and (e1,e2) in the relation *)
let relonInRel e1 e2 r =
  e1 IN r.relon_set &&
  e2 IN r.relon_set &&
  Relation.inRel e1 e2 r.relon_rel

(* return the set of elements that are left of e in r *)
let relonLeftOf e r =
  { a | forall ((a, b) IN r.relon_rel) | b = e }

(* return the set of elements that are right of e in r *)
let relonRightOf e r =
  { b | forall ((a, b) IN r.relon_rel) | a = e }

let relonAddToSet e r = <| r with relon_set = Set.insert e r.relon_set; |>

let relonAddToRel e1 e2 r =
  <| relon_set = r.relon_set union {e1; e2};
     relon_rel = Relation.relAdd e1 e2 r.relon_rel; |>

(* add e to the relation to the right of all the elements for which pred is true *)
let relonAddToTheRight pred e r =
  let relon_rel' =
    r.relon_rel
    union
    {(e', e) | forall (e' IN r.relon_set) | pred e'}
  in
  <| relon_set = Set.insert e r.relon_set;
     relon_rel = relon_rel';
  |>

(* add e to the relation to the left of all the elements for which pred is true *)
let relonAddToTheLeft e pred r =
  let relon_rel' =
    r.relon_rel
    union
    {(e, e') | forall (e' IN r.relon_set) | pred e'}
  in
  <| relon_set = Set.insert e r.relon_set;
     relon_rel = relon_rel';
  |>

let relonRemoveElement e r =
  <| relon_set = r.relon_set \ {e};
     relon_rel = {(a, b) | forall ((a, b) IN r.relon_rel)
                         | a <> e && b <> e};
  |>

let relonFilterSet f r =
  let relon_set' = Set.filter f r.relon_set in
  <| relon_set = relon_set';
     relon_rel = Relation.relRestrict r.relon_rel relon_set';
  |>

let relonUnion r r' =
  <| relon_set = r.relon_set union r'.relon_set;
     relon_rel = r.relon_rel union r'.relon_rel;
  |>

let relonTransitiveClosure r =
  <| r with relon_rel = Relation.transitiveClosure r.relon_rel |>

let relonBigUnion relons =
  <| relon_set = Set.bigunion (Set.map (fun r -> r.relon_set) relons);
     relon_rel = Set.bigunion (Set.map (fun r -> r.relon_rel) relons) |>

let rec list_to_rel_aux (acc : relon 'a) (xs : list 'a) : relon 'a =
  match xs with
  | [] -> acc
  | x :: xs -> 
     let acc = relonAddToTheRight (fun _ -> true) x acc in
     list_to_rel_aux acc xs
  end

let list_to_rel (xs : list 'a) : Relation.rel 'a 'a =
  (list_to_rel_aux relonEmpty xs).relon_rel


(** relon end *)

let fst3 (x,_,_) = x
let snd3 (_,y,_) = y
let thd3 (_,_,z) = z
